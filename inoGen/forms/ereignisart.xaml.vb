Imports System.Data
Imports System.Data.OleDb

Public Class ereignisart
    Private connectionString As String = String.Format("Provider=Microsoft.ACE.OLEDB.12.0;Data Source=""{0}"";", My.Settings.DBPath)

    Private dt As New DataTable()

    Private isNewRecord As Boolean = False
    Private ID As Integer? = Nothing

    Public Sub New()
        InitializeComponent()

        LoadData()
    End Sub

    Private Sub LoadData()
        Try
            Using conn As New OleDbConnection(connectionString)
                conn.Open()
                Dim cmd As New OleDbCommand("SELECT * FROM tblEreignisArt ORDER BY Reihenfolge", conn)
                Dim adapter As New OleDbDataAdapter(cmd)
                dt.Clear()
                adapter.Fill(dt)
            End Using

            dgEreignisArt.ItemsSource = dt.DefaultView
        Catch ex As Exception
            MessageBox.Show("Fehler: " & ex.Message)
        End Try

        If ID.HasValue Then
            For Each rowView As DataRowView In dgEreignisArt.Items
                If CInt(rowView("tblEreignisArtID")) = ID Then
                    ' Selektion setzen
                    dgEreignisArt.SelectedItem = rowView

                    ' Sichtbar machen
                    dgEreignisArt.ScrollIntoView(rowView)

                    Exit For
                End If
            Next
        End If
    End Sub

    Private Sub dgEreignisArt_MouseDoubleClick(sender As Object, e As MouseButtonEventArgs)
        Dim rowView As DataRowView = CType(dgEreignisArt.SelectedItem, DataRowView)
        If rowView IsNot Nothing Then
            ID = Convert.ToInt32(rowView("tblEreignisArtID"))
            txtEreignis.Text = rowView("EreignisArt").ToString()
            txtReihenfolge.Text = rowView("Reihenfolge").ToString()
            txtZeichen.Text = rowView("Zeichen").ToString()
            ckbPerson.IsChecked = CBool(rowView("PersonenEreignis"))
            isNewRecord = False
        End If
    End Sub

    Private Sub btnSave_Click(sender As Object, e As RoutedEventArgs) Handles btnSave.Click
        Dim strInsert As String = "INSERT INTO tblEreignisArt (EreignisArt, Reihenfolge, Zeichen, PersonenEreignis)
            VALUES (?, ?, ?, ?)"
        Dim strUpdate As String = "UPDATE tblEreignisArt SET EreignisArt = ?, Reihenfolge = ?, Zeichen = ?, PersonenEreignis = ? WHERE tblEreignisArtID = ?"

        Try
            Using conn As New OleDbConnection(connectionString)
                conn.Open()
                Dim cmd As New OleDbCommand(If(isNewRecord, strInsert, strUpdate), conn)
                ' Add parameters in the same order as in the SQL statement
                cmd.Parameters.AddWithValue("EreignisArt", txtEreignis.Text)
                cmd.Parameters.AddWithValue("Reihenfolge", txtReihenfolge.Text)
                cmd.Parameters.AddWithValue("Zeichen", txtZeichen.Text)

                cmd.Parameters.AddWithValue("PersonenEreignis", ckbPerson.IsChecked = True)

                If Not isNewRecord AndAlso ID.HasValue Then
                    cmd.Parameters.AddWithValue("tblVKHID", ID.Value)
                End If
                cmd.ExecuteNonQuery()

                If isNewRecord Then
                    Using cmdId As New OleDbCommand("SELECT @@IDENTITY", conn)
                        ID = Convert.ToInt32(cmdId.ExecuteScalar())
                    End Using
                End If

                conn.Close()
                MessageBox.Show("Datensatz erfolgreich " & If(isNewRecord, "erstellt", "aktualisiert") & ".")
                isNewRecord = False
                LoadData()
            End Using

        Catch ex As Exception
            MessageBox.Show("Fehler beim Speichern: " & ex.Message)
        End Try
    End Sub

    Private Sub btnNew_Click(sender As Object, e As RoutedEventArgs) Handles btnNew.Click
        txtEreignis.Clear()
        txtReihenfolge.Clear()
        txtZeichen.Clear()
        ckbPerson.IsChecked = False
        ID = Nothing
        isNewRecord = True
        txtEreignis.Focus()
    End Sub


    Private Sub dgEreignisArt_AutoGeneratedColumns(sender As Object, e As EventArgs) Handles dgEreignisArt.AutoGeneratedColumns
        For Each col In dgEreignisArt.Columns
            If col.Header IsNot Nothing AndAlso String.Equals(col.Header.ToString(), "tblEreignisArtID", StringComparison.OrdinalIgnoreCase) Then
                col.Visibility = Visibility.Collapsed
            End If
        Next
        dgEreignisArt.IsReadOnly = True
        dgEreignisArt.CanUserAddRows = False
        dgEreignisArt.CanUserDeleteRows = False
    End Sub
End Class
