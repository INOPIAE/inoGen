Imports System.Data
Imports System.Data.OleDb

Public Class kreise
    Private connectionString As String =
    String.Format("Provider=Microsoft.ACE.OLEDB.12.0;Data Source=""{0}"";", My.Settings.DBPath)

    Private dt As New DataTable()

    Private isNewRecord As Boolean = False
    Private ID As Integer? = Nothing


    Public Sub New()
        InitializeComponent()

        LoadData()

    End Sub

    Private Sub btnSave_Click(sender As Object, e As RoutedEventArgs) Handles btnSave.Click
        Dim rowView As DataRowView = CType(dgKreise.SelectedItem, DataRowView)
        If rowView IsNot Nothing Or isNewRecord Then
            ' Werte in DataRow ändern


            ' Änderungen in Access speichern
            Try
                Using conn As New OleDbConnection(connectionString)
                    conn.Open()
                    If isNewRecord Then
                        Dim insertCmd As New OleDbCommand("INSERT INTO tblKreis (Kreis, Info) VALUES (?, ?)", conn)
                        insertCmd.Parameters.AddWithValue("@Kreis", txtKreis.Text)
                        insertCmd.Parameters.AddWithValue("@Info", txtInfo.Text)
                        insertCmd.ExecuteNonQuery()
                        MessageBox.Show("Neuer Datensatz gespeichert!")
                    ElseIf ID.HasValue Then
                        ' VORHANDENEN Datensatz updaten
                        rowView("Kreis") = txtKreis.Text
                        rowView("Info") = txtInfo.Text
                        Dim updateCmd As New OleDbCommand("UPDATE tblKreis SET Kreis = ?, Info = ? WHERE tblKreisID = ?", conn)
                        updateCmd.Parameters.AddWithValue("@Kreis", txtKreis.Text)
                        updateCmd.Parameters.AddWithValue("@Info", txtInfo.Text)
                        updateCmd.Parameters.AddWithValue("@ID", ID.Value)
                        updateCmd.ExecuteNonQuery()
                        MessageBox.Show("Änderungen gespeichert!")
                    End If
                End Using

                LoadData() ' DataGrid aktualisieren
            Catch ex As Exception
                MessageBox.Show("Fehler beim Speichern: " & ex.Message)
            End Try
        End If
    End Sub

    Private Sub LoadData()
        Try
            Using conn As New OleDbConnection(connectionString)
                conn.Open()
                Dim cmd As New OleDbCommand("SELECT * FROM tblKreis ORDER BY Kreis", conn)
                Dim adapter As New OleDbDataAdapter(cmd)
                dt.Clear()
                adapter.Fill(dt)
            End Using

            dgKreise.ItemsSource = dt.DefaultView

        Catch ex As Exception
            MessageBox.Show("Fehler: " & ex.Message)
        End Try

        If ID.HasValue Then
            For Each rowView As DataRowView In dgKreise.Items
                If CInt(rowView("tblKreisID")) = ID Then
                    ' Selektion setzen
                    dgKreise.SelectedItem = rowView

                    ' Sichtbar machen
                    dgKreise.ScrollIntoView(rowView)

                    Exit For
                End If
            Next
        End If
    End Sub

    Private Sub btnNew_Click(sender As Object, e As RoutedEventArgs) Handles btnNew.Click
        txtKreis.Clear()
        txtInfo.Clear()
        ID = Nothing
        isNewRecord = True
        txtKreis.Focus()
    End Sub


    Private Sub dgKreise_AutoGeneratedColumns(sender As Object, e As EventArgs) Handles dgKreise.AutoGeneratedColumns
        For Each col In dgKreise.Columns
            If col.Header IsNot Nothing AndAlso String.Equals(col.Header.ToString(), "tblKreisID", StringComparison.OrdinalIgnoreCase) Then
                col.Visibility = Visibility.Collapsed
            End If
        Next
        dgKreise.IsReadOnly = True
        dgKreise.CanUserAddRows = False
        dgKreise.CanUserDeleteRows = False
    End Sub

    Private Sub dgKreise_MouseDoubleClick(sender As Object, e As MouseButtonEventArgs)
        Dim rowView As DataRowView = CType(dgKreise.SelectedItem, DataRowView)
        If rowView IsNot Nothing Then
            ID = Convert.ToInt32(rowView("tblKreisID"))
            txtKreis.Text = rowView("Kreis").ToString()
            txtInfo.Text = rowView("Info").ToString()
            isNewRecord = False
        End If
    End Sub
End Class
