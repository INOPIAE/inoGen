Imports System.Data
Imports System.Data.OleDb

Public Class SuchePerson

    ' Inherits Window


    Public Property SelectedID As Integer? = Nothing

    Public Event PersonSelected(id As Integer)

    Private blnStart As Boolean = True
    Private connectionString As String =
        String.Format("Provider=Microsoft.ACE.OLEDB.12.0;Data Source=""{0}"";", My.Settings.DBPath)
    Private dt As New DataTable()
    Public Sub New()
        InitializeComponent()
        LoadData()
        blnStart = False
    End Sub
    Private Sub LoadData()
        Dim strSQL As String = "SELECT 
                tblPerson.tblPersonID, 
                tblPerson.PS, 
                tblNachname.Nachname, 
                tblPerson.Vorname, 
                tblPerson.Sex, 
                IIf([tblPerson]![tblFamilieID]>0,""X"","""") AS Kind, tblPerson.Info,
                tblPerson.FSID
            FROM tblPerson LEFT JOIN tblNachname ON tblPerson.tblNachnameID = tblNachname.tblNachnameID
            ORDER BY tblPerson.PS;"
        Try
            Using conn As New OleDbConnection(connectionString)
                conn.Open()
                Dim cmd As New OleDbCommand(strSQL, conn)
                Dim adapter As New OleDbDataAdapter(cmd)
                dt.Clear()
                adapter.Fill(dt)
                dgPerson.ItemsSource = dt.DefaultView
            End Using
        Catch ex As Exception
            MessageBox.Show("Fehler beim Laden der Daten: " & ex.Message)
        End Try
    End Sub
    Private Sub btnSearch_Click(sender As Object, e As RoutedEventArgs)
        FilterSetzen()
    End Sub

    Private Sub FilterSetzen()
        If blnStart Then Exit Sub
        Dim filter As String = vbNullString
        Dim sep As String = vbNullString
        If txtNachname.Text.Trim() <> vbNullString Then
            filter = String.Format("Nachname LIKE '%{0}%'", txtNachname.Text.Trim())
            sep = " AND "
        End If
        If txtVorname.Text.Trim() <> vbNullString Then
            filter &= sep & String.Format(" Vorname LIKE '%{0}%'", txtVorname.Text.Trim())
            sep = " AND "
        End If
        If CType(cbSex.SelectedItem, ComboBoxItem).Content.ToString() <> " " Then
            filter &= sep & String.Format(" Sex = '{0}'", CType(cbSex.SelectedItem, ComboBoxItem).Content.ToString())
            sep = " AND "
        End If
        If ckbKind.IsChecked.Value Then
            filter &= sep & " Kind <> 'X'"
        End If
        If Not String.IsNullOrEmpty(filter) Then
            Dim dv As New DataView(dt)
            dv.RowFilter = filter
            dgPerson.ItemsSource = dv
        Else
            dgPerson.ItemsSource = dt.DefaultView
        End If
    End Sub

    Private Sub btnOK_Click(sender As Object, e As RoutedEventArgs)

        If dgPerson.SelectedItem IsNot Nothing Then
            Dim row As DataRowView = CType(dgPerson.SelectedItem, DataRowView)
            Dim selectedId As Integer = CInt(row("tblPersonID"))

            ' Event auslösen → Rückgabe an Hauptfenster
            RaiseEvent PersonSelected(selectedId)

            ' Fenster schließen
            Me.Close()
        Else
            MessageBox.Show("Bitte einen Datensatz auswählen.")
        End If
    End Sub

    Private Sub btnClear_Click(sender As Object, e As RoutedEventArgs)
        txtVorname.Clear()
        txtNachname.Clear()
        cbSex.SelectedIndex = 0
        ckbKind.IsChecked = False
        FilterSetzen()
    End Sub

    Private Sub cbSex_SelectionChanged(sender As Object, e As SelectionChangedEventArgs) Handles cbSex.SelectionChanged
        FilterSetzen()
    End Sub

    Private Sub txtNachname_TextChanged(sender As Object, e As TextChangedEventArgs) Handles txtNachname.TextChanged
        FilterSetzen()
    End Sub

    Private Sub txtVorname_TextChanged(sender As Object, e As TextChangedEventArgs) Handles txtVorname.TextChanged
        FilterSetzen()
    End Sub

    Private Sub ckbKind_Click(sender As Object, e As RoutedEventArgs) Handles ckbKind.Click
        FilterSetzen()
    End Sub

    Private Sub dgPerson_AutoGeneratedColumns(sender As Object, e As EventArgs) Handles dgPerson.AutoGeneratedColumns
        For Each col In dgPerson.Columns
            If col.Header IsNot Nothing Then
                Select Case col.Header.ToString()
                    Case "tblPersonID" ', "tblFamilieID", "tblNachnameID", "tblKonfessionID"
                        col.Visibility = Visibility.Collapsed
                End Select
            End If
        Next

        dgPerson.IsReadOnly = True
        dgPerson.CanUserAddRows = False
    End Sub

    Private Sub dgPerson_MouseDoubleClick(sender As Object, e As MouseButtonEventArgs) 'Handles dgPerson.MouseDoubleClick
        If dgPerson.SelectedItem IsNot Nothing Then
            Dim row As DataRowView = CType(dgPerson.SelectedItem, DataRowView)
            Dim selectedId As Integer = CInt(row("tblPersonID"))

            ' Event auslösen → Rückgabe an Hauptfenster
            RaiseEvent PersonSelected(selectedId)

            ' Fenster schließen
            Me.Close()
        Else
            MessageBox.Show("Bitte einen Datensatz auswählen.")
        End If
    End Sub
End Class
