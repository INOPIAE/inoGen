Imports System.Data
Imports System.Data.OleDb
Imports inoGenDLL

Public Class orte
    Private connectionString As String = String.Format("Provider=Microsoft.ACE.OLEDB.12.0;Data Source=""{0}"";", My.Settings.DBPath)

    Private dt As New DataTable()

    Private isNewRecord As Boolean = False
    Private ID As Integer? = Nothing

    Private cOSM As New ClsOSMKarte
    Public Sub New()
        InitializeComponent()

        LoadData()
        LoadKreisListe()
        cOSM.Email = My.Settings.Email
    End Sub

    Private Sub btnSave_Click(sender As Object, e As RoutedEventArgs) Handles btnSave.Click
        Dim rowView As DataRowView = CType(dgOrte.SelectedItem, DataRowView)
        If rowView IsNot Nothing Or isNewRecord Then

            Try
                Using conn As New OleDbConnection(connectionString)
                    conn.Open()
                    If isNewRecord Then
                        Dim insertCmd As New OleDbCommand("INSERT INTO tblOrt (Ort, tblKreisID, Info, Breite, Laenge) VALUES (?, ?, ?, ?, ?)", conn)
                        insertCmd.Parameters.AddWithValue("@Ort", txtOrt.Text)
                        insertCmd.Parameters.AddWithValue("@Kreisid", cbKreis.SelectedValue)
                        insertCmd.Parameters.AddWithValue("@Info", txtInfo.Text)
                        insertCmd.Parameters.AddWithValue("@Breite", If(String.IsNullOrWhiteSpace(txtLat.Text), DBNull.Value, CType(txtLat.Text, Double)))
                        insertCmd.Parameters.AddWithValue("@Laenge", If(String.IsNullOrWhiteSpace(txtLon.Text), DBNull.Value, CType(txtLon.Text, Double)))
                        insertCmd.ExecuteNonQuery()
                        MessageBox.Show("Neuer Datensatz gespeichert!")
                    ElseIf ID.HasValue Then
                        Dim updateCmd As New OleDbCommand("UPDATE tblOrt SET Ort = ?, tblKreisID = ?, Info = ?, Breite = ?, Laenge = ? WHERE tblOrtID = ?", conn)
                        updateCmd.Parameters.AddWithValue("@Ort", txtOrt.Text)
                        updateCmd.Parameters.AddWithValue("@Kreisid", cbKreis.SelectedValue)
                        updateCmd.Parameters.AddWithValue("@Info", txtInfo.Text)
                        updateCmd.Parameters.AddWithValue("@Breite", If(String.IsNullOrWhiteSpace(txtLat.Text), DBNull.Value, CType(txtLat.Text, Double)))
                        updateCmd.Parameters.AddWithValue("@Laenge", If(String.IsNullOrWhiteSpace(txtLon.Text), DBNull.Value, CType(txtLon.Text, Double)))
                        updateCmd.Parameters.AddWithValue("@ID", ID.Value)
                        updateCmd.ExecuteNonQuery()
                        MessageBox.Show("Änderungen gespeichert!")
                    End If
                End Using

                LoadData()
            Catch ex As Exception
                MessageBox.Show("Fehler beim Speichern: " & ex.Message)
            End Try
        End If
    End Sub

    Private Sub LoadData()
        Try
            Using conn As New OleDbConnection(connectionString)
                conn.Open()
                '  Dim cmd As New OleDbCommand("SELECT * FROM tblOrt ORDER BY Ort", conn)
                Dim cmd As New OleDbCommand("SELECT o.*, k.Kreis FROM tblOrt as o, tblKreis as k WHERE o.tblKreisID = k.tblKreisID ORDER BY o.Ort", conn)
                Dim adapter As New OleDbDataAdapter(cmd)
                dt.Clear()
                adapter.Fill(dt)
            End Using

            dgOrte.ItemsSource = dt.DefaultView

        Catch ex As Exception
            MessageBox.Show("Fehler: " & ex.Message)
        End Try

        If ID.HasValue Then
            For Each rowView As DataRowView In dgOrte.Items
                If CInt(rowView("tblOrtID")) = ID Then
                    ' Selektion setzen
                    dgOrte.SelectedItem = rowView

                    ' Sichtbar machen
                    dgOrte.ScrollIntoView(rowView)

                    Exit For
                End If
            Next
        End If
    End Sub

    Private Sub btnNew_Click(sender As Object, e As RoutedEventArgs) Handles btnNew.Click
        txtOrt.Clear()
        cbKreis.SelectedValue = 1
        txtInfo.Clear()
        txtLat.Clear()
        txtLon.Clear()
        ID = Nothing
        isNewRecord = True
        txtOrt.Focus()
    End Sub


    Private Sub dgOrte_AutoGeneratedColumns(sender As Object, e As EventArgs) Handles dgOrte.AutoGeneratedColumns
        For Each col In dgOrte.Columns
            If col.Header IsNot Nothing AndAlso String.Equals(col.Header.ToString(), "tblOrtID", StringComparison.OrdinalIgnoreCase) Then
                col.Visibility = Visibility.Collapsed
            End If
            If col.Header IsNot Nothing AndAlso String.Equals(col.Header.ToString(), "tblKreisID", StringComparison.OrdinalIgnoreCase) Then
                col.Visibility = Visibility.Collapsed
            End If
        Next
        dgOrte.IsReadOnly = True
        dgOrte.CanUserAddRows = False
        dgOrte.CanUserDeleteRows = False
    End Sub

    Private Sub dgOrte_MouseDoubleClick(sender As Object, e As MouseButtonEventArgs)
        Dim rowView As DataRowView = CType(dgOrte.SelectedItem, DataRowView)
        If rowView IsNot Nothing Then
            ID = Convert.ToInt32(rowView("tblOrtID"))
            txtOrt.Text = rowView("Ort").ToString()
            cbKreis.SelectedValue = Convert.ToInt32(rowView("tblKreisID"))
            txtInfo.Text = rowView("Info").ToString()
            txtLat.Text = If(rowView("Breite") IsNot DBNull.Value, rowView("Breite").ToString(), "")
            txtLon.Text = If(rowView("Laenge") IsNot DBNull.Value, rowView("Laenge").ToString(), "")
            isNewRecord = False
        End If
    End Sub

    Private Sub cbKreis_SelectionChanged(sender As Object, e As SelectionChangedEventArgs) Handles cbKreis.SelectionChanged
        If cbKreis.SelectedValue IsNot Nothing Then
            Dim selectedID As Integer = CInt(cbKreis.SelectedValue)
        End If
    End Sub

    Private Sub LoadKreisListe()
        Try
            Dim dt As New DataTable()
            Using conn As New OleDbConnection(connectionString)
                conn.Open()
                Dim cmd As New OleDbCommand("SELECT tblKreisID, Kreis FROM tblKreis ORDER BY Kreis", conn)

                Dim adapter As New OleDbDataAdapter(cmd)
                adapter.Fill(dt)
            End Using

            cbKreis.ItemsSource = dt.DefaultView

        Catch ex As Exception
            MessageBox.Show("Fehler beim Laden der Kreise: " & ex.Message)
        End Try
    End Sub

    Private Sub btnGeo_Click(sender As Object, e As RoutedEventArgs) ' Handles btnGeo.Click
        Dim results = cOSM.FindGeoCode(txtOrt.Text)
        If results IsNot Nothing AndAlso results.Count > 0 Then
            For Each r In results
                Dim test As Integer = MessageBox.Show($"Ergebnis: {r.display_name} (lat: {r.lat}, lon: {r.lon}) Gefundene Einträge ({results.Count})", "Ort", MessageBoxButton.YesNoCancel)
                If test = MessageBoxResult.Yes Then
                    txtLat.Text = r.lat.Replace(".", ",")
                    txtLon.Text = r.lon.Replace(".", ",")
                    txtInfo.Text &= r.display_name
                    Exit For
                ElseIf test = MessageBoxResult.Cancel Then
                    Exit For
                End If
            Next
        Else
            MessageBox.Show("Kein Ergebnis gefunden.")
        End If
    End Sub
End Class
